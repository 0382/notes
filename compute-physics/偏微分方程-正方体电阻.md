# 偏微分方程——正方体电阻

## 问题提出

知乎上有一个很有趣的问题：[将一个正方体电阻平均分为九份，挖掉中间一块，现在的电阻是多少？](https://www.zhihu.com/question/40508117)。

想要数值计算任意形状的电阻，只能上有限元软件了。不过，如果是一些挖的比较正的情况，我们还是可以算一算的。

### 微分方程

首先我们建立以下微分方程。电流守恒
$$
\nabla \cdot \vec{j} + \dfrac{\partial \rho}{\partial t} = 0
$$
电流稳定时，电流密度不随时间变化，所以$\nabla\cdot\vec{j}=0$。 然后是电流密度和场强的关系
$$
\vec{j} = \sigma \vec{E},
$$
以及场强和电势的关系
$$
\nabla \varphi = -\vec{E}.
$$
所以最终的方程为
$$
0 = \nabla \cdot (\sigma \vec{E}) = \nabla\cdot(-\sigma \nabla \varphi) = -\sigma \nabla^2\varphi
$$
即$\nabla^2\varphi = 0$。

### 边界条件

在考虑边界条件之前，我们假定正方体的变长为$L$，放在$[0, L]\times[0, L]\times[0, L]$的三维坐标之内，该材料的电导率为$\sigma$。

假设我们要求的问题是正方体给定的两个对面有稳定的电势，不妨设
$$
\varphi(x = 0) = V, \quad \varphi(x = L) = 0
$$

如果不挖掉一块，那么电阻很好算
$$
R = \dfrac{\rho L}{S} = \dfrac{1}{\sigma}\dfrac{L}{L^2} = \dfrac{1}{\sigma L}
$$

现在挖掉一块之后，电阻只好按照欧姆定律来求了
$$
R = \dfrac{V}{I} = \dfrac{V}{\int_{x = 0} \vec{j}\cdot d\vec{s}}
$$
其中，电流是对一个电阻面电流密度$\vec{j}$的积分，而想要求出电流密度分布，就需要该面上的场强（也即电势的梯度）分布。

除了和电极连接的两个面，剩下的表面没有电流流出和流入，即$\vec{E}\cdot d\vec{s} = 0$。

不过直接考虑三维体系计算量太大，对边界的处理也过于繁琐。这里我们进件考虑平行于$z$轴挖穿的情况。此时平行于$z$轴的每条线都是等势的，我们仅需要考虑一个二维的微分方程即可。

### 离散化

除了前面的要求外，我们还要求挖掉部分的也是方方正正平行于坐标轴的，这种情况下，就可以用方格来离散化我们的微分方程了。

假设我们取一系列离散的方格点$(x_i, y_j$，并使用缩写$\varphi_{ij} = \varphi(x_i, y_j)$。那么$\nabla^2\varphi = 0$可以离散化为
$$
\begin{aligned}
0 &= \nabla^2\varphi = (\dfrac{\partial^2}{\partial x^2} + \dfrac{\partial^2}{\partial y^2})\varphi \\
&= \dfrac{\varphi_{i+1,j} + \varphi_{i-1,j}-2\varphi_{ij}}{(\Delta x)^2} + \dfrac{\varphi_{i,j+1}+\varphi_{i,j-1}-2\varphi_{ij}}{(\Delta y)^2}
\end{aligned}
$$
如果我们对$x,y$取离散格点长度一样大，即$h = \Delta x = \Delta y$，那么方程可以写为
$$
\varphi_{i+1,j} + \varphi_{i-1,j} + \varphi_{i,j+1}+\varphi_{i,j-1} - 4\varphi_{ij} = 0.
$$

数值求解这个方程，一个非常直观的方法是将其化为
$$
\boldsymbol{A}\boldsymbol{\varphi} = \boldsymbol{b}
$$
的线性方程组，其中$\boldsymbol{A}$就是$\nabla^2$算符，比如其对角元都是$-4$，$\boldsymbol{\varphi}$是所有格点上的电势组成的向量，$\boldsymbol{b}$在这里也并不是零向量，通常我们会把边界条件产生的一些杂七杂八的边界量放到$\boldsymbol{b}$里面。在这种解法中，$\boldsymbol{A}$是一个对称稀疏矩阵，可以采用共轭梯度法等迭代算法来进行计算。

不过这种方法还涉及到将二维格点展成一位向量，对于边界的处理也非常繁琐。我们这里采用另外一种简单直观且好用的方法：Jacobi算法。

将上述离散化的方程改写为
$$
\varphi_{ij} = \dfrac{1}{4}(\varphi_{i+1,j} + \varphi_{i-1,j} + \varphi_{i,j+1}+\varphi_{i,j-1})
$$
Jacobi算法要做的事情就是，先随便设定一个初始的电势分布$\varphi_{ij}$，然后代入上式的右边计算出下一步的$\varphi_{ij}$，不断循环直到电势分布稳定下来。不仅算法非常简单，而且他对于边界的处理也相对简便。对此算法有一个比较直观的理解：边界以及源（本题中没有源）的影响相当于在不断的迭代过程中，被“扩散”到整个场之中。

尽管如此，对于边界的处理也要一个一个分别讨论，实际上边界的处理也是数值求解偏微分方程最为重要且繁琐的地方。

考虑$y = L$的截面，此时$y_j + h$在界面外，边界条件要求电势法向梯度为零，我们这里采取如下处理方式：
$$
\dfrac{\varphi_{i,j+1} - \varphi_{i,j-1}}{2h} = 0
$$
当然此时$\varphi_{i,j+1}$并不存在，我们的目的是借助这个关系把Jacobi迭代的方程改写为
$$
\varphi_{ij} = \dfrac{1}{4}(\varphi_{i+1,j} + \varphi_{i-1,j} + 2\varphi_{i,j-1})
$$
其余的截面采用类似的处理方式。

让我们首先考虑在正方体中间打个洞，即挖掉$[L/3, 2L/3]\times [L/3, 2L/3]\times [0, L]$部分的情况。

我们给每个$L$长度离散成$3N+1$个点，计算代码如下：
```julia
using OffsetArrays
const N = 100

function case_1()
    # 挖掉部分 [L/3, 2L/3] × [L/3, 2L/3]
    # julia 从 1 开始的索引在这里确实难受，用 `OffsetArrays` 包装一下
    ϕ0 = OffsetArray(zeros(3N+1, 3N+1), 0:3N, 0:3N)
    δϕ = copy(ϕ0)

    # 初值设为没有挖掉时的电势分布，便于更快收敛
    for i = 0:3N
        @. ϕ0[i, :] = 1 - i/3N
    end
    ϕ1 = copy(ϕ0)

    max_diff = 1.
    iter_num = 0
    while max_diff > 1e-10
        for j = 1:3N-1
            for i = 1:3N-1
                N <= i <= 2N && N <= j <= 2N && continue # 挖掉的部分
                @inbounds ϕ1[i,j] = (ϕ0[i-1,j]+ϕ0[i+1,j]+ϕ0[i,j-1]+ϕ0[i,j+1])/4
            end
        end
        # 处理边界条件
        for i = 1:3N-1
            # y = 0 处
            @inbounds ϕ1[i, 0] = (ϕ0[i-1,0]+ϕ0[i+1,0]+2ϕ0[i,1])/4
            # y = L 处
            @inbounds ϕ1[i, 3N] = (ϕ0[i-1,3N]+ϕ0[i+1,3N]+2ϕ0[i,3N-1])/4
        end
        # 处理挖掉部分的边界条件
        for i = (N+1):(2N-1)
            # y = L/3
            @inbounds ϕ1[i, N] = (ϕ0[i-1,N]+ϕ0[i+1,N]+2ϕ0[i,N-1])/4
            # y = 2L/3
            @inbounds ϕ1[i, 2N] = (ϕ0[i-1,2N]+ϕ0[i+1,2N]+2ϕ0[i,2N+1])/4
        end
        for j = (N+1):(2N-1)
            # x = L/3
            @inbounds ϕ1[N, j] = (ϕ0[N,j-1]+ϕ0[N,j+1]+2ϕ0[N-1,j])/4
            # x = 2L/3
            @inbounds ϕ1[2N, j] = (ϕ0[2N,j-1]+ϕ0[2N,j+1]+2ϕ0[2N+1,j])/4
        end
        # 内部的四个顶点
        ϕ1[N,N] = (ϕ0[N-1,N]+ϕ0[N+1,N]+ϕ0[N,N-1]+ϕ0[N,N+1])/4
        ϕ1[N,2N] = (ϕ0[N-1,2N]+ϕ0[N+1,2N]+ϕ0[N,2N-1]+ϕ0[N,2N+1])/4
        ϕ1[2N,N] = (ϕ0[2N-1,N]+ϕ0[2N+1,N]+ϕ0[2N,N-1]+ϕ0[2N,N+1])/4
        ϕ1[2N,2N] = (ϕ0[2N-1,2N]+ϕ0[2N+1,2N]+ϕ0[2N,2N-1]+ϕ0[2N,2N+1])/4
        @. δϕ = abs(ϕ1 - ϕ0)
        max_diff = maximum(δϕ)
        ϕ0, ϕ1 = ϕ1, ϕ0
        iter_num += 1
    end
    println("Jabobi algorithm converge after $iter_num iterations")
    E = @. 3N * (ϕ0[0, :] - ϕ0[1, :])
    # 令 σ = 1, 则 j = E
    I = sum(E[1:3N]) / 3N
    R = 1 / I
    return R
end
```
计算得到$R \approx 1.274586$。
