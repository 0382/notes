# 中心力场的薛定谔方程

一维薛定谔方程的计算基本讲的差不多了，接下来我们介绍一下三维中心力场下的薛定谔方程计算。

考虑一个三维中心力场的哈密顿量
$$
\hat{H} = \dfrac{p^2}{2m} + V(r).
$$
通常的量子力学书上都有对它的讨论，我们不再赘述。通过分离变量法，波函数可以写为
$$
\phi_{nlm}(r, \theta, \phi) = R_{nl}(r) Y^m_l(\theta, \phi).
$$
其中$Y^m_l(\theta, \phi)$是球谐函数，径向波函数$R_{nl}(r)$由如下的径向薛定谔方程决定
$$
\dfrac{\mathrm{d}^2}{\mathrm{d}r^2} R(r) + \dfrac{2}{r}\dfrac{\mathrm{d}}{\mathrm{d}r} R(r) + \left[\dfrac{2m}{\hbar^2}(E-V(r)) - \dfrac{l(l+1)}{r^2}\right]R(r) = 0.
$$

## 自旋轨道耦合

在进一步计算之前，我们先讨论一下自旋轨道耦合。它加入上上述计算体系中并不复杂，同时也是实际计算中非常重要的一个话题。（虽然不再是中心力了）。

考虑一个包含自旋轨道耦合的哈密顿量
$$
\hat{H} = \dfrac{p^2}{2m} + V(r) + \zeta(r)\vec{l}\cdot \vec{s}.
$$
此时$l_z$不再是好量子数，而$\vec{l}^2, \vec{j}^2, j_z, \vec{s}^2$都是好量子数。本征波函数为
$$
    \psi_{nljm_j}(r,\theta,\phi) = R_{nlj}(r)Y^{ls}_{jm_j}(\theta,\phi).
$$
其中$Y^{ls}_{jm_j}(\theta,\phi)$称为**张量球谐函数**（tensor spherical harmonics），其定义为
$$
    Y^{ls}_{jm_j}(\theta,\phi) = \sum_{m,\sigma} C^{jm_j}_{lms\sigma}Y_{l}^m(\theta,\phi)\chi_{s\sigma}.
$$
根据
$$
\vec{l}\cdot\vec{s} = \dfrac{1}{2}(\vec{j}^2 - \vec{l}^2 - \vec{s}^2)
$$
我们不难得到径向波函数的薛定谔方程
$$
    \dfrac{\mathrm{d}^2}{\mathrm{d}r^2} R_{lj}(r) + \dfrac{2}{r}\dfrac{\mathrm{d}}{\mathrm{d}r} R_{lj}(r) + \left[\dfrac{2m}{\hbar^2}(E-V_{lj}(r)) - \dfrac{l(l+1)}{r^2}\right]R_{lj}(r) = 0.
$$
其中
$$
V_{lj}(r) = V(r) + \dfrac{\hbar^2}{2}[j(j+1) - l(l+1) - s(s+1)]\zeta(r).
$$
它与不含自旋轨道耦合的薛定谔方程看上去区别不大，只是不同分波的相互作用有一些差别而已。

## 一些预处理

通常，令
$$
E = \dfrac{\hbar^2 k^2}{2m},  \quad v_{lj}(r) = \dfrac{2mV_{lj}(r)}{\hbar^2}, \quad u(r) = \dfrac{R_{lj}(r)}{r}.
$$
我们得到径向方程
$$
u''(r) + \left[k^2 - v_{lj}(r) - \dfrac{l(l+1)}{r^2}\right] u(r) = 0.
$$
当$r\to 0$时，如果有$r^2V(r) \to 0$，那么一定有（曾谨言《量子力学（卷一）》）
$$
    u(r) \propto r^{(l+1)}.
$$
这个渐进行为是计算中非常重要的一个出发点。

## 求解微分方程

一个很有意思的事情是，根据上述提到的渐进行为，我们完全可以计算散射态，即$k^2>0$的态。由于散射态总是存在的，设置一个$k^2$然后采取求解微分方程的套路，用龙格库塔算法什么之类的，就可以计算出波函数$u(r)$。

不过，这样积分到无穷远处，误差自然是会越来越大的。因此更加有效的计算，通常还需要考察$r\to\infty$处的渐进行为，然后从两边分别求解。当存在库伦力时，渐进行为是所谓的库伦函数，处理起来相当复杂，我也没仔细研究过，就不班门弄斧了。

对于束缚态，其实也可以用类似的方式求解，随意一个$k^2 < 0$，然后使用龙格库塔求解微分方程，看波函数是否束缚态（在远处趋向于零）。这样就可以采取二分法搜索的方式，寻找合适的$k^2$。这个方法被称为打靶法。

不过正如前面说的，积分到$r$很大的时候是容易有误差的，有的时候很难判断$u(r)$是否是束缚态。

## 求解薛定谔方程

总之，为了求解束缚态，采用矩阵对角化总是比较不错的一个方法。所以我们继续采用这个方法，将$r \in [0, R_0]$离散化为$r_i = ih$，于是令$u_i = u(r_i)$，有
$$
\dfrac{2u_i - u_{i+1} - u_{i-1}}{h^2} + [v_{lj}(r_i) + \dfrac{l(l+1)}{(ih)^2}]u_i = k^2 u_i.
$$
这相当于求解久期方程
$$\boldsymbol{A}\boldsymbol{u} = k^2 \boldsymbol{u}.$$
其中
$$
\boldsymbol{A} = \begin{pmatrix}
    d_1 & e \\ 
    e & d_2 & e \\ 
      & e & d_3 & e \\
      & & \ddots & \ddots & \ddots \\
      & & & e & d_{n-1} & e \\
      & & & & e & d_n 
\end{pmatrix}
, \quad \boldsymbol{u} = \begin{pmatrix}
    u_1 \\ u_2 \\ \vdots \\ u_{n-1} \\ u_n
\end{pmatrix}
$$
这里根据前面的渐进行为，有$u_0 = 0$，同时我们假设在充分大的远处$u(R_0 + h) = 0$，这对于束缚态来说是大致成立的。这里$d_i$和$e$矩阵元为
$$
\begin{aligned}
d_i &= \dfrac{2}{h^2} + v_{lj}(r_i) + \dfrac{l(l+1)}{(ih)^2} \\
e &= -\dfrac{1}{h^2}
\end{aligned}
$$
实际上做法与一维薛定谔方程的求解基本上一致。

### 例子

下面以原子核物理中常见的Woodx-Saxon势为例展示计算方法。一个以Woods-Saxon势为基础的平均场哈密顿量通常可以写为
$$
    h = \dfrac{p^2}{2m} + V_\text{WS}(r) + V_\text{C}(r) + V_\text{LS}(r)\vec{l}\cdot\vec{s}.
$$
其中Woods-Saxon势的形式为
$$
    V_\text{WS} = \dfrac{-V_0}{1 + e^{(r-R)/a}}.
$$
一个常用的参数为[^1]
$$
\begin{aligned}
    R &= r_0 A^{1/3} = 1.27A^{1/3} \mathrm{fm} \quad \text{（原子核半径）} \\
    a &= 0.67 \mathrm{fm} \quad \text{（表面弥散）} \\
    V_0 &= \left(51 \pm 33\dfrac{N-Z}{A}\right) \mathrm{MeV}.
\end{aligned}
$$
其中$V_0$的$+$号和$-$号分别对应于质子和中子受到的势能。如果不区分质子和中子，通常也可以采用$V_0 = 57 \mathrm{MeV}$。

库伦势为
$$
    V_\text{C}(r) = \dfrac{Ze^2}{4\pi\epsilon} \begin{cases}
        \dfrac{3-(r/R)^2}{2R}, & r \leq R, \\
        \dfrac{1}{r}, & r > R,
    \end{cases}
$$
$R$的取值同Woods-Saxon势参数。自旋轨道耦合项为
$$
    V_\text{LS}(r) = V^{(0)}_\text{LS} \left(\dfrac{r_0}{\hbar}\right)^2 \dfrac{1}{r} \dfrac{\mathrm{d}}{\mathrm{d}r}\dfrac{1}{1+e^{(r-R)/a}}.
$$
所有参数同Woods-Saxon势，且自旋耦合强度通常取[^1]
$$
    V^{(0)}_\text{LS} = 0.44 V_0.
$$

计算代码如下
```julia
using LinearAlgebra

const m = 939 # Mev/c^2
const ħ = 197.326 # MeV c fm

N = 8 # 中子数
Z = 8 # 质子数
A = N + Z # 核子数

ħω = 41 / cbrt(A) # MeV
α = √(m * ħω) / ħ


@show ħω
@show α

function Vws(r)
    V0 = 51
    a = 0.67 # fm
    R = 1.27cbrt(A)
    return -V0 / (1 + exp((r - R)/a))
end

function Vc(r)
    R = 1.27cbrt(A)
    r ≤ R && return 1.44 * Z * (3 - (r/R)^2) / 2R
    r > R && return 1.44 * Z / r
end

function Vls(r)
    V0 = 51
    a = 0.67 # fm
    R = 1.27cbrt(A)
    Vls0 = 0.44V0 * 1.27^2
    t = exp((r-R)/a)
    return -Vls0 / (r*a) * t / (1 + t)^2
end

"""
    solve_ws_space(l::Integer, dj::Integer)
l: 轨道角动量
dj: 总角动量的两倍（因为总角动量是半整数）
"""
function solve_ws_space(l::Integer, dj::Integer)
    j = dj / 2.0
    V(r) = begin
        # 想计算中子，删掉 Vc 项就好了
        Vws(r) + Vc(r) + 0.5(j*(j+1) - l*(l+1) - 3/4)*Vls(r)
    end

    Δx = 0.1
    N = 100
    x = collect(1:N) * Δx
    efactor = ħ^2 / 2m
    d = @. 2efactor / (Δx)^2 + (V(x) + efactor * l*(l+1)/(x^2))
    e = fill(-efactor/(Δx)^2, N-1)
    H = SymTridiagonal(d, e)
    vals, vecs = eigen(H, V(0), 0)
    for (i, e) in enumerate(vals)
        println("$(i)th bound state is $(e)MeV")
    end
end
```
束缚态的能量与参考书上的结果是基本能够对上的。

[^1] J. Suhonen, From nucleons to nucleus: concepts of microscopic nuclear theory, Theoretical and mathematical physics (Springer, Berlin ; New York, 2007), 645 pp